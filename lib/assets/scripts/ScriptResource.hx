package assets.scripts;

import js.Browser;
import js.html.Event;
import js.lib.Error;
import js.html.ScriptElement;

/**
 * –°–∫—Ä–∏–ø—Ç JS.  
 * –û–±—ä–µ–∫—Ç –≤—ã–ø–æ–ª–Ω—è–µ—Ç –ø–æ–¥–∫–ª—é—á–µ–Ω–∏–µ –≤–Ω–µ—à–Ω–µ–≥–æ JavaScript –∫–æ–¥–∞.
 * –ö–æ–¥ –¥–æ–±–∞–≤–ª—è–µ—Ç—Å—è –≤ —Ä–∞–∑–¥–µ–ª `head` —Ç–µ–∫—É—â–µ–π web —Å—Ç—Ä–∞–Ω–∏—Ü—ã.
 */
class ScriptResource extends Resource<ScriptResource, ScriptParams>
{
    /**
     * –°–æ–∑–¥–∞—Ç—å –∑–∞–≥—Ä—É–∑—á–∏–∫ JavaScript –∫–æ–¥–∞.
     * @param manager –†–æ–¥–∏—Ç–µ–ª—å—Å–∫–∏–π –º–µ–Ω–µ–¥–∂–µ—Ä –∑–∞–≥—Ä—É–∑—á–∏–∫–∞.
     * @param params –ü–∞—Ä–∞–º–µ—Ç—Ä—ã –¥–ª—è —Å–æ–∑–¥–∞–Ω–∏—è —Ä–µ—Å—É—Ä—Å–∞.
     */
    private function new(manager:ScriptsManager, params:ScriptParams) {
        super(manager, params.id==null?params.url:params.id, ResourceType.SCRIPT, params);

        if (params.url == null)
            throw new Error("The url of script cannot be null");

        this.url = params.url;
        this.async = params.async==null?null:params.async;
        this.defer = params.defer==null?null:params.defer;
        this.crossOrigin = params.crossOrigin==null?null:params.crossOrigin;
        this.script = Browser.document.createScriptElement();
    }

    /**
     * URL –ê–¥—Ä–µ—Å —Ä–∞—Å–ø–æ–ª–æ–∂–µ–Ω–∏—è –ø–æ–¥–∫–ª—é—á–∞–µ–º–æ–≥–æ —Å–∫—Ä–∏–ø—Ç–∞.  
     * –ù–µ –º–æ–∂–µ—Ç –±—ã—Ç—å `null`.
     */
    public var url(default, null):String;

    /**
     * –¢–µ–≥ `script` –¥–ª—è –∑–∞–≥—Ä—É–∂–∞–µ–º–æ–≥–æ –∫–æ–¥–∞ JavaScript.  
     * –ù–µ –º–æ–∂–µ—Ç –±—ã—Ç—å `null`
     */
    public var script(default, null):ScriptElement;

    /**
     * –ü–æ–ª–∏—Ç–∏–∫–∞ –±–µ–∑–æ–ø–∞—Å–Ω–æ—Å—Ç–∏ [CORS](https://developer.mozilla.org/en-US/docs/Web/HTTP/CORS)
     * –¥–ª—è –∑–∞–≥—Ä—É–∑–∫–∏ —ç—Ç–æ–≥–æ JavaScript.  
     * –°–º–æ—Ç—Ä–∏—Ç–µ: https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/crossorigin
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `null`
     */
    public var crossOrigin(default, null):String;

    /**
     * –§–ª–∞–≥ –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è –¥–æ –∑–∞–≥—Ä—É–∑–∫–∏ DOM.
     * @see https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `null`
     */
    public var defer:Bool;

    /**
     * –§–ª–∞–≥ –∞—Å–∏–Ω—Ö—Ä–æ–Ω–Ω–æ–≥–æ –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è.
     * @see https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `null`
     */
    public var async:Bool;

    /**
     * –ù–∞—á–∞—Ç—å –∑–∞–≥—Ä—É–∑–∫—É.  
     */
    override private function load():Void {
        if (isLoading || isComplete || isDisposed)
            return;

        isLoading = true;

        if (async != null)          script.async = async;
        if (defer != null)          script.defer = defer;
        if (crossOrigin != null)    script.crossOrigin = crossOrigin;

        script.addEventListener("load", onLoad);
        script.addEventListener("error", onLoadError);
        script.src = url;

        if (Browser.document.head == null)
            Browser.document.addEventListener("DOMContentLoaded", onDOMLoaded);
        else
            onDOMLoaded();
    }

    private function onDOMLoaded():Void {
        Browser.document.removeEventListener("DOMContentLoaded", onDOMLoaded);
        Browser.document.head.appendChild(script);
    }

    private function onLoad(e:Event):Void {
        isComplete = true;
        isLoading = false;

        script.removeEventListener("load", onLoad);
        script.removeEventListener("error", onLoadError);

        onComplete.emit(this);
    }

    private function onLoadError(e:Event):Void {
        isComplete = true;
        isLoading = false;

        error = new Error("Failed to load script resource: " + url);

        script.removeEventListener("load", onLoad);
        script.removeEventListener("error", onLoadError);

        onComplete.emit(this);
    }

    /**
     * –£–Ω–∏—á—Ç–æ–∂–∏—Ç—å —ç—Ç–æ—Ç —Å–∫—Ä–∏–ø—Ç. üî•  
     * –í—ã–∑–æ–≤ –∏–≥–Ω–æ—Ä–∏—Ä—É–µ—Ç—Å—è, –µ—Å–ª–∏ —ç—Ç–æ—Ç —Ä–µ—Å—É—Ä—Å —É–∂–µ –±—ã–ª –≤—ã–≥—Ä—É–∂–µ–Ω.
     * - –í—ã–≥—Ä—É–∂–∞–µ—Ç –≤—Å–µ –¥–∞–Ω–Ω—ã–µ, —Å–≤—è–∑–∞–Ω–Ω—ã–µ —Å —ç—Ç–∏–º —Ä–µ—Å—É—Ä—Å–æ–º.
     * - –£–¥–∞–ª—è–µ—Ç —Å–∫—Ä–∏–ø—Ç —Å–æ —Å—Ç—Ä–∞–Ω–∏—Ü—ã.
     * - –û—á–∏—â–∞–µ—Ç —Å–ø–∏—Å–æ–∫ —Å–ª—É—à–∞—Ç–µ–ª–µ–π `onComplete=null`.
     * - –£–¥–∞–ª—è–µ—Ç –æ–±—ä–µ–∫—Ç –∏–∑ —Ä–æ–¥–∏—Ç–µ–ª—å—Å–∫–æ–≥–æ –º–µ–Ω–µ–¥–∂–µ—Ä–∞ `manager=null`.
     * - –£–¥–∞–ª—è–µ—Ç –æ—à–∏–±–∫—É `error=null`.
     * - –£—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ—Ç —Å–≤–æ–π—Å—Ç–≤–æ `isDisposed=true`.
     * 
     * –í—ã –±–æ–ª—å—à–µ –Ω–µ –¥–æ–ª–∂–Ω—ã –∏—Å–ø–æ–ª—å–∑–æ–≤–∞—Ç—å —ç—Ç–æ—Ç –æ–±—ä–µ–∫—Ç!
     */
    override public function dispose():Void {
        if (isDisposed)
            return;

        if (script != null) {
            if (script.parentNode != null)
                script.parentNode.removeChild(script);

            script.removeEventListener("load", onLoad);
            script.removeEventListener("error", onLoadError);
            script = null;
        }

        Browser.document.removeEventListener("DOMContentLoaded", onDOMLoaded);
        super.dispose();
    }

    /**
     * –ü–æ–ª—É—á–∏—Ç—å —Å—Ç—Ä–æ–∫–æ–≤–æ–µ –æ–ø–∏—Å–∞–Ω–∏–µ —ç—Ç–æ–≥–æ —ç–∫–∑–µ–º–ø–ª—è—Ä–∞.
     * @return –°—Ç—Ä–æ–∫–æ–≤–æ–µ –ø—Ä–µ–¥—Å—Ç–∞–≤–ª–µ–Ω–∏–µ –æ–±—ä–µ–∫—Ç–∞.
     */
    @:keep
    override public function toString():String {
        return "[ScriptResource id=" + id + "]";
    }
}

/**
 * –ü–∞—Ä–∞–º–µ—Ç—Ä—ã –¥–ª—è —Å–æ–∑–¥–∞–Ω–∏—è –ø–æ–¥–∫–ª—é—á–∞–µ–º–æ–≥–æ —Å–∫—Ä–∏–ø—Ç–∞.
 */
typedef ScriptParams =
{
    /**
     * ID –†–µ—Å—É—Ä—Å–∞.  
     * –ï—Å–ª–∏ –Ω–µ –∑–∞–¥–∞–Ω, –±—É–¥–µ—Ç –∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω–æ –∑–Ω–∞—á–µ–Ω–∏–µ **url**.
     */
    @:optional var id:String;

    /**
     * URL –ê–¥—Ä–µ—Å –¥–ª—è –∑–∞–≥—Ä—É–∑–∫–∏ –¥–∞–Ω–Ω—ã—Ö.  
     * –ù–µ –º–æ–∂–µ—Ç –±—ã—Ç—å `null`
     */
    var url:String;

    /**
     * –ü–æ–ª–∏—Ç–∏–∫–∞ –±–µ–∑–æ–ø–∞—Å–Ω–æ—Å—Ç–∏ [CORS](https://developer.mozilla.org/en-US/docs/Web/HTTP/CORS)
     * –¥–ª—è –∑–∞–≥—Ä—É–∑–∫–∏ —ç—Ç–æ–≥–æ JavaScript.  
     * –°–º–æ—Ç—Ä–∏—Ç–µ: https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/crossorigin
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `null`
     */
    @:optional var crossOrigin:String;

    /**
     * –§–ª–∞–≥ –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è –¥–æ –∑–∞–≥—Ä—É–∑–∫–∏ DOM.
     * @see https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `null`
     */
    @:optional var defer:Bool;

    /**
     * –§–ª–∞–≥ –∞—Å–∏–Ω—Ö—Ä–æ–Ω–Ω–æ–≥–æ –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è.
     * @see https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script
     * 
     * –ü–æ —É–º–æ–ª—á–∞–Ω–∏—é: `null`
     */
    @:optional var async:Bool;
}